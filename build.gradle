buildscript {
	repositories {
		mavenCentral()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
		classpath "com.diffplug.spotless:spotless-plugin-gradle:$spotlessVersion"
	}
}

apply plugin: 'org.springframework.boot'

description = "Ohmmx-Root"

// 子项目统一配置
subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'com.diffplug.spotless'

	group = 'com.ohmmx'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	[compileJava, compileTestJava]*.options.collect { options ->
		options.encoding = 'UTF-8'
	}

	spotless {
		java {
			importOrder 'java', 'javax', 'org', 'com', ''
			removeUnusedImports()
			lineEndings 'UNIX'
			encoding 'UTF-8'
			eclipse().configFile "${project(':ohmmx-archive-config').projectDir}/spotless-style.xml"
			trimTrailingWhitespace()
			endWithNewline()
		}
		format 'misc', {
			target '**/.gitignore', '**/*.gradle', '**/*.md', '**/*.sh'
			indentWithTabs()
			lineEndings 'UNIX'
			encoding 'UTF-8'
			trimTrailingWhitespace()
			endWithNewline()
		}
		format 'yaml', {
			target '**/*.yml', '**/*.yaml'
			indentWithSpaces()
			lineEndings 'UNIX'
			encoding 'UTF-8'
			trimTrailingWhitespace()
			endWithNewline()
		}
	}

	if(project.name != 'ohmmx-archive-config' && project.name != 'ohmmx-archive-center') {
		dependencies {
			modules {}
			implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
			implementation 'org.springframework.boot:spring-boot-starter-data-redis'
			implementation 'org.springframework.boot:spring-boot-starter-jdbc'
			implementation 'org.springframework.boot:spring-boot-starter-quartz'
			implementation 'org.springframework.boot:spring-boot-starter-web'
			implementation 'org.springframework.boot:spring-boot-starter-validation'
			implementation 'org.flywaydb:flyway-core'
			implementation 'org.flywaydb:flyway-mysql'

			implementation "com.google.code.gson:gson:$googleGsonVersion"
			implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
			implementation "com.zaxxer:HikariCP:$zaxxerHikariCpVersion"
			implementation "joda-time:joda-time:$jodaTimeVersion"
			implementation "org.springdoc:springdoc-openapi-ui:$springdocVersion"
			implementation "redis.clients:jedis:$jedisVersion"

			// JDK1.8需要单独引入
			implementation "org.apache.commons:commons-lang3:$commonsLang3Version"

			runtimeOnly 'com.mysql:mysql-connector-j'

			testImplementation 'org.springframework.boot:spring-boot-starter-test'

			if(project.name != 'ohmmx-archive-common') {
				implementation project(':ohmmx-archive-common')
			}
		}
	}

	tasks.named('test') {
		useJUnitPlatform()
	}

	repositories {
		mavenCentral()
		maven { url "https://plugins.gradle.org/m2/" }
	}
}
